def shouldSpotbugs =
    !project.hasProperty('no-spotbugs') &&
    (neo4jDefault == project.properties.getOrDefault("neo4jVersion", "not-set"))

if (shouldSpotbugs) {
    allprojects { proj ->

        plugins.withType(JavaLibraryPlugin) {

            proj.apply plugin: 'com.github.spotbugs'

            proj.spotbugs {
                toolVersion = ver.spotbugsToolVersion
                effort = 'max'
                reportLevel = 'low'

                // When  `ignoreFailures=false` the build will be failing on spotbugs errors
                ignoreFailures = false
                showStackTraces = false
                showProgress = false

                excludeFilter = file("$publicDir/etc/spotbugs/spotbugs-exclude.xml")

                dependencies {
                    spotbugsPlugins group: 'com.mebigfatguy.sb-contrib', name: 'sb-contrib', version: ver.'sb-contrib'
                }
            }

            proj.tasks.withType(SpotBugsTask) {
                proj.tasks.spotbugs.dependsOn(it)

                def taskName = it.name
                it.reports {
                    html {
                        // Due to a known issue with SpotBugs, HTML and XML reports cannot be generated both at the same time.
                        enabled = true
                        destination = file("${spotbugsReportsDir}/html/${taskName}/${proj.name}.html")
                    }
                    xml {
                        enabled = false
                        destination = file("${spotbugsReportsDir}/xml/${taskName}/${proj.name}.xml")
                    }
                }
            }
        }
    }
}
